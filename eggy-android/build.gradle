plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'signing'
}

group = "com.eggybytes"

// Specified in gradle.properties
version = version

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30

        buildConfigField("String", "VERSION_NAME", "\"$version\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments["clearPackageData"] = "true"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.firebase:firebase-messaging:20.1.6'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                credentials {
                    username = mavenCentralUsername
                    password = mavenCentralPassword
                }
            }
        }

        publications {
            release(MavenPublication) {
                from components.release

                artifactId = "eggy-android"

                pom {
                    name = "eggy SDK for Android"
                    description = "Official eggy SDK for Android"
                    url = "https://github.com/eggybytes/eggy-android"

                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://opensource.org/licenses/MIT"
                        }
                    }

                    organization {
                        name = "eggy"
                        url = "https://useeggy.com/"
                    }

                    developers {
                        developer {
                            id = "eggy"
                            name = "eggy Team"
                            email = "hello@useeggy.com"
                        }
                    }

                    scm {
                        connection = "scm:git:git://github.com/eggybytes/eggy-android.git"
                        developerConnection = "scm:git:ssh:git@github.com:eggybytes/eggy-android.git"
                        url = "https://github.com/eggybytes/eggy-android"
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.release
    }
}